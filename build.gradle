plugins {
    id "java"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.7.0"
    id "checkstyle"
    id "com.palantir.git-version" version "0.12.3"
}

repositories {
    mavenCentral()
}

group = "io.trocco"
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^v[0-9]+\.[0-9]+\.[0-9]+([.-][.a-zA-Z0-9-]+)?/) {
        vd.lastTag.substring(1)
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()
description = "Salesforce Einstein Analytics (Wave) input plugin for Embulk"

sourceCompatibility = 1.8
targetCompatibility = 1.8

def embulkVersion = "0.10.42"

dependencies {
    compileOnly "org.embulk:embulk-api:${embulkVersion}"
    compileOnly "org.embulk:embulk-spi:${embulkVersion}"

    implementation "org.embulk:embulk-util-config:0.3.4"
    implementation "org.embulk:embulk-util-json:0.2.2"
    implementation "org.embulk:embulk-util-timestamp:0.2.2"
    implementation "org.embulk:embulk-util-file:0.1.5"

    // https://mvnrepository.com/artifact/com.force.api/force-wsc
    implementation group: 'com.force.api', name: 'force-wsc', version: '47.0.0'
    // https://mvnrepository.com/artifact/com.force.api/force-partner-api
    implementation group: 'com.force.api', name: 'force-partner-api', version: '47.0.0'
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.embulk:embulk-core:${embulkVersion}"
    testImplementation "org.embulk:embulk-deps:${embulkVersion}"
    testImplementation "org.embulk:embulk-junit4:${embulkVersion}"
}

embulkPlugin {
    mainClass = "org.embulk.input.salesforce_ea.SalesforceEaFileInputPlugin"
    category = "input"
    type = "salesforce_ea"
}

gem {
    authors = ["dododo8m"]
    email = ["taichiddt+8m@gmail.com"]
    summary = "Salesforce Einstein Analytics file input plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-input-salesforce_ea"
    licenses = ["MIT"]
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

dependencyLocking {
    lockAllConfigurations()
}
